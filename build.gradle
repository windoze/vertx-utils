buildscript {
    ext {
        vertx_version = "3.6.2"
        ch_qos_logback_version = '1.2.3'
    }
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id "org.jetbrains.dokka" version "0.9.17"
    id "com.jfrog.bintray" version "1.8.4"
}

group 'codes.unwritten'
version '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin-coroutines:$vertx_version"
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-web-client:$vertx_version"
    compile "ch.qos.logback:logback-classic:$ch_qos_logback_version"
    compile "net.logstash.logback:logstash-logback-encoder:5.2"
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8'
    testCompile "io.vertx:vertx-unit:$vertx_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}
dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}
task kotlinDocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            from components.java
            artifact kotlinDocJar
            artifact sourceJar

            groupId 'codes.unwritten'
            artifactId 'vertx-common'
            version '0.1'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.apikey') ? project.property('bintray.apikey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenKotlin']
    publish = true

    pkg {
        repo = 'maven'
        name = "vertx-utils"
        desc = "Some Vertx/Kotlin Utils"
        websiteUrl = "https://github.com/windoze/vertx-utils"
        issueTrackerUrl = "https://github.com/windoze/vertx-utils/issues"
        vcsUrl = "https://github.com/windoze/vertx-utils.git"
        licenses = ['WTFPL']
        publicDownloadNumbers = true
        version {
            name = "0.1"
            desc = "Vertx Utils 0.1"
            released = new Date()
            vcsTag = "0.1.0"
        }
    }
}

